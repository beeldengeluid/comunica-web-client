# MOZ Concerts - standardised locations
# Datasource: https://cat.apis.beeldengeluid.nl/sparql
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdo: <https://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# Shows the ID and label of standardised locations associated with concerts 
# that are part of the Dutch Broadcast Concert (MOZ) collection 
# This does not include locations that are not standardised

SELECT DISTINCT ?standardLocationEntity ?locationLabel
WHERE
{
   ?programUri sdo:partOfSeason/sdo:partOfSeries <http://data.beeldengeluid.nl/id/series/2101608030025711131> .
  {
    ?programUri sdo:locationCreated ?location .
    ?location sdo:sameAs ?standardLocationEntity . 
    ?location sdo:name ?locationLabel
  }
  UNION
  {
    ?programUri sdo:hasPart ?sceneUri .
    ?sceneUri sdo:locationCreated ?location .
    ?location sdo:sameAs ?standardLocationEntity . 
    ?location sdo:name ?locationLabel
  }
} ORDER BY ?locationLabel