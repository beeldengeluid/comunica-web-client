# MOZ Concerts linked to a person
# Datasource: https://cat.apis.beeldengeluid.nl/sparql
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdo: <https://schema.org/>

# Shows the ID and title for concerts part of the Dutch Broadcast Concert (MOZ) collection and
# are linked to a person (in this example Beethoven), in alphabetical order.
# The person could be linked in the role of Artist, Creator, or the concert may be about the person.
# Persons can be linked at either program (concert) level or at scene level (a scene within the concert)

SELECT DISTINCT ?programUri ?programName 
WHERE
{
  VALUES ?person_id { <http://data.beeldengeluid.nl/gtaa/80808> }  # GTAA URI for Beethoven
 # Filter for programmes belonging to the series "Muziekopnamen Zendgemachtigden (MOZ)", using its ID
 ?programUri sdo:partOfSeason/sdo:partOfSeries <http://data.beeldengeluid.nl/id/series/2101608030025711131> ;
             sdo:name ?programName 
  # look for links at program level
  {
    ?programUri (sdo:byArtist|sdo:creator)/(sdo:byArtist|sdo:creator) ?person_id .
  }
  UNION
  {	
    ?programUri sdo:about ?person_id .
  }
  UNION
  # look for links at scene level
  {	
    ?programUri sdo:hasPart/(sdo:byArtist|sdo:creator)/(sdo:byArtist|sdo:creator) ?person_id .
  }
  UNION
  {
    ?programUri sdo:hasPart/sdo:about ?person_id .
  }
} ORDER BY ?programName
