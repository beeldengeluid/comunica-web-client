# Select persons related to a program (also via series/season/scene)
# Datasource: https://cat.apis.beeldengeluid.nl/sparql
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdo: <https://schema.org/>

# Select persons related to a program (also via series/season/scene)
SELECT ?entity ?entityName
WHERE {
  VALUES ?program_uri {<http://data.beeldengeluid.nl/id/program/2101608160143012931>}
  {
    ?program_uri a sdo:CreativeWork ;
      (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer)/
      (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer) ?entity  .
    ?entity skos:prefLabel ?entityName ;
            skos:inScheme <http://data.beeldengeluid.nl/gtaa/Persoonsnamen>
  }
  UNION
  {
    ?program_uri sdo:isPartOfSeason ?season .
    ?season (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer)/
    (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer) ?entity  .
    ?entity skos:prefLabel ?entityName ;
            skos:inScheme <http://data.beeldengeluid.nl/gtaa/Persoonsnamen>
  }
  UNION
  {
    ?program_uri sdo:isPartOfSeason/sdo:isPartOfSeries ?series .
    ?series (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer)/
    (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer) ?entity  .
    ?entity skos:prefLabel ?entityName ;
            skos:inScheme <http://data.beeldengeluid.nl/gtaa/Persoonsnamen>
  }
  UNION
  {
    ?scene sdo:hasPart ?program_uri.
    ?scene (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer)/
    (sdo:about|sdo:creator|sdo:contributor|sdo:actor|sdo:crew|sdo:performer) ?entity  .
    ?entity skos:prefLabel ?entityName ;
            skos:inScheme <http://data.beeldengeluid.nl/gtaa/Persoonsnamen>
  }
}