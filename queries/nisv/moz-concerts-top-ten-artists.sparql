# MOZ Concerts - top 10 artists
# Datasource: https://cat.apis.beeldengeluid.nl/sparql

PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX sdo: <https://schema.org/>

# Shows the name and number of concerts for the top 10 most frequently 
# occurring artists in concerts that are part of the Dutch Broadcast Concert (MOZ) collection
# Note: an artist can be either an individual or a musical group

SELECT ?count ?name
WHERE
{
 {
  SELECT (COUNT(DISTINCT ?programUri) as ?count) ?name
  WHERE
  {
   # Filter for programmes belonging to the series "Muziekopnamen Zendgemachtigden (MOZ)", using its ID
   ?programUri sdo:partOfSeason/sdo:partOfSeries <http://data.beeldengeluid.nl/id/series/2101608030025711131> .
      {
        # concerts with artist at program level
        ?programUri sdo:byArtist/sdo:byArtist ?entity  .
        ?entity skosxl:prefLabel/skosxl:literalForm ?name
      }
      UNION  # OR
      { # concerts with artist at scene level
        ?programUri sdo:hasPart/sdo:byArtist/sdo:byArtist ?entity  .
        ?entity skosxl:prefLabel/skosxl:literalForm ?name
      }
  } GROUP BY ?entity ?name 
 }
} ORDER BY desc(?count) LIMIT 10
